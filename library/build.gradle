apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion Versions.targetSdk

    lintOptions {
        abortOnError false
        checkReleaseBuilds true
        warningsAsErrors true
        disable 'MissingTranslation',
                'GoogleAppIndexingWarning',
                'UnusedResources',
                'ContentDescription',
                'RtlSymmetry',
                'RtlHardcoded',
                'RtlEnabled'
    }

    defaultConfig {
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Frames.version
        versionName Frames.versionName
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true
        proguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
        consumerProguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    buildTypes {
        buildTypes.each {
            it.buildConfigField("String", "DASHBOARD_NAME", "\"Frames\"")
            it.buildConfigField("String", "DASHBOARD_VERSION", "\"$Frames.versionName\"")
        }
        release {
            minifyEnabled false
            proguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
            consumerProguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        pickFirst 'META-INF/core_debug.kotlin_module'
        pickFirst 'META-INF/core_release.kotlin_module'
        pickFirst 'META-INF/library_debug.kotlin_module'
        pickFirst 'META-INF/library_release.kotlin_module'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api Libs.dependencies
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.amitshekhar.android:android-networking:1.0.2'
    // implementation 'com.github.AppIntro:AppIntro:6.0.0'
    implementation 'com.google.android.gms:play-services-ads:19.6.0'
    implementation 'com.google.ads.mediation:unity:3.5.1.1'
    implementation 'com.google.android.ads.consent:consent-library:1.0.8'
    implementation project(path: ':appintro')
    implementation project(path: ':nativetemplates')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    kapt Libs.kaptDependencies
}

apply from: '../lib-artifacts.gradle'
